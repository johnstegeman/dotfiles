# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.

if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

#####################################################################################################
# zinit installer. 

ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

### End of zinit installer's chunk
#####################################################################################################

# Some configuration variables 

export TERM=xterm-256color
ENABLE_CORRECTION="false"
COMPLETION_WAITING_DOTS="true"
export DIRCOLORS_SOLARIZED_ZSH_THEME="256dark"

# set Homebrew environment and path if installed

if [ -x "$(command -v /opt/homebrew/bin/brew)" ]; then
  eval $(/opt/homebrew/bin/brew shellenv)
  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"
  autoload -Uz compinit
  compinit
fi

# Shamelessly stolen from https://github.com/z-shell/playground/blob/main/profiles/NICHOLAS85/.zshrc
zt()  { zinit depth'3' lucid ${1/#[0-9][a-c]/wait"$1"} "${@:2}"; }

autoload -U zmv
bindkey '^X' push-input

zt light-mode blockf for ~/.zsh_custom/config-file

# Powerlevel10k theme

zt light-mode for \
depth:'1' atload:"[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh" nocd \
  romkatv/powerlevel10k

zt light-mode for zdharma-continuum/zinit-annex-meta-plugins @annexes

# Source all custom scripts
source_directory ~/.zsh_custom/autoload

consoleskip='dircolors-material ogham/exa jonas/tig sharkdp'
sharkskip='sharkdp/hexyl sharkdp/vivid'

zi light-mode for \
OMZP::copybuffer \
OMZP::direnv \
OMZL::git.zsh \
OMZP::git \
romkatv/zsh-defer \
ael-code/zsh-colored-man-pages \
skip:$consoleskip @console-tools \
skip:$sharkskip @sharkdp

unset consoleskip
unset sharkdp

#####################################################################################################
# The following use turbo mode to load after the shell prompt is ready
# They are grouped using a, b, c to enforce a load order

zt 0a light-mode for \
atload:'_zsh_autosuggest_start' \
  zsh-users/zsh-autosuggestions \
OMZP::history \
zsh-users/zsh-completions \
hcgraf/zsh-sudo \
zdharma-continuum/zsh-navigation-tools \
zdharma-continuum/zui \
zdharma-continuum/zbrowse

zt 0b light-mode for \
pick:'autopair.zsh' nocompletions atload:'bindkey "^H" backward-kill-word; ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(autopair-insert)' \
  hlissner/zsh-autopair \
autoload:'#manydots-magic' \
  knu/zsh-manydots-magic \
atinit:'zicompinit; zicdreplay' atload:'FAST_HIGHLIGHT[chroma-man]=; fast-theme -q default' \
  zdharma-continuum/fast-syntax-highlighting \
MichaelAquilina/zsh-you-should-use \
has:'eza' atload:'export eza_params=('--time-style=long-iso' '--group-directories-first')' johnstegeman/zsh-eza \
has:'bat' fdellwing/zsh-bat \
has:'aws' OMZP::aws \
has:'chezmoi' johnstegeman/zsh-chezmoi \
pinelibg/dircolors-solarized-zsh \
has:'multipass' OMZP::multipass

zt 0c light-mode binary for \
sbin:'fd*/fd;fd*/fd -> fdfind' from:"gh-r" \
  @sharkdp/fd \
mv:'dua* dua' sbin:'**/dua(.exe|) -> dua' atload:'alias du="dua i"' from:"gh-r"\
  Byron/dua-cli \
mv:'duf* duf' sbin:'**/duf(.exe|) -> duf' atload:'alias df=$(which duf)' from:"gh-r"\
  muesli/duf \
mv:'lnav* lnav' sbin:'**/lnav(.exe|) -> lnav' atload:'alias -s "log"="lnav"'\
  tstack/lnav \
has:'asciinema' mv:'agg* agg' sbin:'**/agg(.exe|) -> agg' from:"gh-r" \
  @asciinema/agg

zt 0c light-mode null for \
sbin \
  paulirish/git-open \
id-as:'Cleanup' nocd atinit:'unset -f zt; _zsh_autosuggest_bind_widgets' \
  zdharma-continuum/null \
pack:'bgn-binary+keys' \
  fzf \
pack:'bgn' \
  fzy 

# end of turbo loading section
#####################################################################################################